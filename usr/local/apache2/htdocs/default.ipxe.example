#!ipxe

# If we have an IP already, show it, or else get one via dhcp
isset netX/ip && show netX/ip || dhcp

# This script derived from https://gist.github.com/robinsmidsrod/2234639

# Global variables used by all other iPXE scripts
chain --autofree menu.ipxe.cfg ||

# Some menu defaults
set menu-timeout 5000
set submenu-timeout ${menu-timeout}
isset ${menu-default} || set menu-default remotecore

# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x64 || set arch x86
cpuid --ext 29 && set archl amd64 || set archl i386

###################### MAIN MENU ####################################

:start
menu iPXE boot menu
item --gap --                    ------------------------- Main Options ------------------------------
item --key m memtest             Memtest86+
item --key e efishell            UEFI Shell
item --key d ipxe_demo           iPXE Demo Script
item --gap --                    ------------------------- Advanced options -------------------------------
item --key c config              Configure settings
item shell                       Drop to iPXE shell
item reboot                      Reboot computer
item
item --key x exit                Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start

############ MAIN MENU ITEMS ############

:memtest
echo Starting memtest from http://${web-server}/memtest
kernel http://${web-server}/memtest/memtest64.efi
boot || goto failed
goto start

:efishell
# X86_64 ONLY - We'll need to get fancy in order to support other architectures with a single menu (see cpuid above)
echo Starting UEFI Shell from http://${web-server}/uefishell
kernel http://${web-server}/uefishell/bootx64.efi
boot || goto failed
goto start

:ipxe_demo
kernel vmlinuz-3.16.0-rc4 bootfile=http://boot.ipxe.org/demo/boot.php fastboot initrd=initrd.img
initrd initrd.img
boot || goto failed
goto start
